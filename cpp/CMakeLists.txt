# Configure code generation

GENERATE_CODE(c++ STABLE)
GENERATE_CODE(c++ SANDBOX STABLE)

# Configure compilation

INCLUDE(PedanticCompilerWarnings)
INCLUDE(InstallFilesRecursive)

SET(STABLE_NAME  "rst")
SET(SANDBOX_NAME "rstsandbox")
SET(SO_VERSION   "${RST_VERSION_MAJOR}.${RST_VERSION_MINOR}")

FOREACH(DOMAIN ${DOMAINS})
    IF(WIN32)
        SET(RST${DOMAIN}_CFLAGS "/DRST${DOMAIN}_EXPORT=__declspec(dllimport)")
    ELSE()
        SET(RST${DOMAIN}_CFLAGS "-DRST${DOMAIN}_EXPORT=")
    ENDIF()
ENDFOREACH()

FIND_PACKAGE(Boost 1.38 REQUIRED thread) # We just need the include directory
INCLUDE_DIRECTORIES(BEFORE SYSTEM ${Boost_INCLUDE_DIRS})

ADD_SUBDIRECTORY(stable)
ADD_SUBDIRECTORY(sandbox)

# --- user options ---

OPTION(EXPORT_TO_CMAKE_PAKAGE_REGISTRY "If set to ON, RST will be exported to the CMake user package registry so that downstream projects automatically find the workspace location in find_package calls." OFF)

FUNCTION(ADD_DOMAIN_LIBRARY DOMAIN)
    SET(DEPENDENCY_DOMAINS ${ARGN})

    STRING(TOLOWER ${DOMAIN} DOMAIN_LOWER)

    # Configure shared library
    INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_BINARY_DIR})

    ADD_DEFINITIONS(${RST${DOMAIN}_CFLAGS})
    FOREACH(DOMAIN ${DEPENDENCY_DOMAINS})
        ADD_DEFINITIONS(${RST${DOMAIN}_CFLAGS})
    ENDFOREACH()

    INCLUDE_DIRECTORIES(BEFORE "${CMAKE_CURRENT_BINARY_DIR}/rst")
    ADD_LIBRARY(${${DOMAIN}_NAME} SHARED ${CPP_${DOMAIN}_SOURCES})
    TARGET_LINK_LIBRARIES(${${DOMAIN}_NAME} ${CMAKE_THREAD_LIBS_INIT})
    FOREACH(DEPDOMAIN ${DEPENDENCY_DOMAINS})
        TARGET_LINK_LIBRARIES(${${DOMAIN}_NAME} ${${DEPDOMAIN}_NAME})
    ENDFOREACH()

    SET_TARGET_PROPERTIES(${${DOMAIN}_NAME}
                          PROPERTIES
                          VERSION ${SO_VERSION})

    # Install library and headers
    INSTALL(TARGETS ${${DOMAIN}_NAME}
            EXPORT  RSTDepends
            ARCHIVE DESTINATION lib
            LIBRARY DESTINATION lib
            RUNTIME DESTINATION bin)

    INSTALL_FILES_RECURSIVE("include/${INSTALL_PREFIX}/${DOMAIN_LOWER}/" ${CPP_${DOMAIN}_HEADERS})

    # Package config file
    CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/${${DOMAIN}_NAME}.pc.in"
                   "${CMAKE_CURRENT_BINARY_DIR}/${${DOMAIN}_NAME}.pc"
                   @ONLY)
    INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/${${DOMAIN}_NAME}.pc"
            DESTINATION lib/pkgconfig)
ENDFUNCTION()

# --- pkgconfig files ---

FOREACH(DOMAIN ${DOMAINS})
    CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/${${DOMAIN}_NAME}.pc.in"
                   "${CMAKE_CURRENT_BINARY_DIR}/${${DOMAIN}_NAME}.${SO_VERSION}pc"
                   @ONLY)
    INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/${${DOMAIN}_NAME}${SO_VERSION}.pc"
            DESTINATION lib/pkgconfig)
ENDFOREACH()

# --- cmake export ---

IF(EXPORT_TO_CMAKE_PAKAGE_REGISTRY)
    MESSAGE(STATUS "Exported RST to CMake package registry")
    EXPORT(PACKAGE RST)
ENDIF()

CONFIGURE_FILE(RSTBuildTreeSettings.cmake.in
               "${CMAKE_CURRENT_BINARY_DIR}/RSTBuildTreeSettings.cmake" @ONLY)

# CMake config files
FILE(TO_CMAKE_PATH "${RSC_DIR}"       RSC_DIR)
FILE(TO_CMAKE_PATH "${PROTOBUF_ROOT}" PROTOBUF_ROOT)

SET(RELATIVE_LINES)
FOREACH(FORMAT ${FORMATS})
    FOREACH(DOMAIN ${DOMAINS})

        STRING(TOLOWER ${FORMAT} FORMAT_LOWER)
        STRING(TOLOWER ${DOMAIN} DOMAIN_LOWER)

        SET(${FORMAT}_${DOMAIN}_FILES_REL)
        FOREACH(FORMAT_FILE ${${FORMAT}_${DOMAIN}_FILES})
            FILE(RELATIVE_PATH FORMAT_FILE_REL "${CMAKE_SOURCE_DIR}/schema/${DOMAIN_LOWER}/${FORMAT_LOWER}" ${FORMAT_FILE})
            LIST(APPEND ${FORMAT}_${DOMAIN}_FILES_REL ${FORMAT_FILE_REL})
        ENDFOREACH()

        SET(RELATIVE_LINES "${RELATIVE_LINES}
SET(RST_${FORMAT}_${DOMAIN}_ROOT \"\${RST_CONFIG_DIR}/schema/${DOMAIN_LOWER}/${FORMAT_LOWER}\")
SET(RST_${FORMAT}_${DOMAIN}_FILES)
FOREACH(F ${${FORMAT}_${DOMAIN}_FILES_REL})
    LIST(APPEND RST_${FORMAT}_${DOMAIN}_FILES \"\${RST_${FORMAT}_${DOMAIN}_ROOT}/\${F}\")
ENDFOREACH()")

    ENDFOREACH()
ENDFOREACH()
FOREACH(TARGET ${MAPPING_TARGETS})
    FOREACH(DOMAIN ${DOMAINS})

        STRING(TOLOWER ${TARGET} TARGET_LOWER)
        STRING(TOLOWER ${DOMAIN} DOMAIN_LOWER)

        SET(MAPPING_${TARGET}_${DOMAIN}_FILES_REL)
        FOREACH(TARGET_FILE ${MAPPING_${TARGET}_${DOMAIN}_FILES})
            FILE(RELATIVE_PATH TARGET_FILE_REL "${CMAKE_SOURCE_DIR}/schema/${DOMAIN_LOWER}/mapping/${TARGET_LOWER}" ${TARGET_FILE})
            LIST(APPEND MAPPING_${TARGET}_${DOMAIN}_FILES_REL ${TARGET_FILE_REL})
        ENDFOREACH()

        SET(RELATIVE_LINES "${RELATIVE_LINES}
SET(RST_MAPPING_${TARGET}_${DOMAIN}_ROOT \"\${RST_CONFIG_DIR}/schema/${DOMAIN_LOWER}/mapping/${TARGET_LOWER}\")
SET(RST_MAPPING_${TARGET}_${DOMAIN}_FILES)
FOREACH(F ${MAPPING_${TARGET}_${DOMAIN}_FILES_REL})
    LIST(APPEND RST_MAPPING_${TARGET}_${DOMAIN}_FILES \"\${RST_MAPPING_${TARGET}_${DOMAIN}_ROOT}/\${F}\")
ENDFOREACH()")

    ENDFOREACH()
ENDFOREACH()

CONFIGURE_FILE(RSTConfig.cmake.in
               "${CMAKE_CURRENT_BINARY_DIR}/RSTConfig.cmake"
               @ONLY)
CONFIGURE_FILE(RSTConfigVersion.cmake.in
               "${CMAKE_CURRENT_BINARY_DIR}/RSTConfigVersion.cmake"
               @ONLY)

# Install rules
INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/RSTConfig.cmake"
               DESTINATION share/${INSTALL_PREFIX})
INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/RSTConfigVersion.cmake"
               DESTINATION share/${INSTALL_PREFIX})
EXPORT(TARGETS ${STABLE_NAME} ${SANDBOX_NAME} FILE "${CMAKE_CURRENT_BINARY_DIR}/RSTDepends.cmake")
INSTALL(EXPORT RSTDepends
        DESTINATION "share/${INSTALL_PREFIX}")
